
plugins {
  id 'checkstyle'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.1.6'
  id 'org.springframework.boot' version '2.7.18'
  id 'org.owasp.dependencycheck' version '9.2.0'
  id 'com.github.ben-manes.versions' version '0.46.0'
  id 'org.sonarqube' version '4.0.0.2929'
  id 'java-library'
  id 'maven-publish'
}

def buildNumber = System.getenv("RELEASE_VERSION") ?: "DEV-SNAPSHOT"

group = 'com.github.hmcts'
version = buildNumber

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

def versions = [
  junit        : '5.7.0',
  junitPlatform: '1.8.1',
  lombok :       '1.18.26'
]

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

// https://github.com/gradle/gradle/issues/16791
tasks.withType(JavaExec).configureEach {
  javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

tasks.withType(Test) {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

configurations.all {
  exclude group:"org.bouncycastle", module: "bcprov-jdk15on"
  exclude group:"org.bouncycastle", module: "bcutil-jdk15on"
  exclude group:"org.bouncycastle", module: "bcpkix-jdk15on"
  exclude group:"org.json", module: "json:20231013"
  exclude group: "org.slf4j", module: "slf4j-log4j12"
  exclude group: "org.slf4j", module: "slf4j-simple"
  exclude group: "org.slf4j", module: "jul-to-slf4j"
  exclude group: "org.slf4j", module: "log4j-over-slf4j"
  exclude group: "ch.qos.logback", module: "logback-classic"
  exclude group: "ch.qos.logback", module: "logback-core"
}


test {
  failFast = true
}

checkstyle {
  maxWarnings = 0
  toolVersion = '10.7.0'
  getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
}

jacocoTestReport {
  executionData(test)
  reports {
    xml.required = true
    csv.required = false
    html.required = true
  }
}

project.tasks['sonarqube'].dependsOn jacocoTestReport

sonarqube {
  properties {
    property "sonar.projectName", "Reform :: Civil Commons Library"
    property "sonar.projectKey", "uk.gov.hmcts.reform:spring-boot-template"
  }
}

// before committing a change, make sure task still works
dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

dependencyManagement {
  dependencies {

    // Attempt to resolve CVE-2023-51775 (Repo Maintainer must fix)
    dependency group: 'org.bitbucket.b_c', name: 'jose4j', version: '0.9.6'

    // Attempt to resolve CVE-2022-45688 and Solves CVE-2023-5072  (Repo Maintainer must fix)
    dependency group: 'org.json', name: 'json', version: '20240303'

    // Solves CVE-2024-34447, CVE-2024-29857, CVE-2024-29858, CVE-2024-30171, CVE-2023-33202
    dependency group: 'org.bouncycastle', name: 'bcpkix-jdk18on', version: '1.79'

    // Solves CVE-2023-24998
    dependency group: 'commons-fileupload', name: 'commons-fileupload', version: '1.5'

    // Solve CVE-2024-22243
    dependency group: 'org.springframework', name: 'spring-core', version: '5.3.39'

    //Solves CVE-2023-35116
    dependencySet(group: 'com.fasterxml.jackson.core', version: '2.16.0') {
      entry 'jackson-databind'
      entry 'jackson-core'
      entry 'jackson-annotations'
    }

  }
  imports {
    mavenBom 'org.springframework.cloud:spring-cloud-dependencies:2020.0.6'
  }
}
// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 0-10 fails the build, anything greater and it doesn't fail the build
  failBuildOnCVSS = 0
  suppressionFile = 'config/owasp/suppressions.xml'

  analyzers {
    // Disable scanning of .NET related binaries
    assemblyEnabled = false
  }
  skipConfigurations = [
    "checkstyle",
    "compileOnly",
  ]
}

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url "https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1"
  }
}

ext {
  log4JVersion = "2.19.0"
}

ext['snakeyaml.version'] = '2.0'
ext.libraries = [
  junit5: [
    "org.junit.jupiter:junit-jupiter-api:${versions.junit}",
    "org.junit.jupiter:junit-jupiter-engine:${versions.junit}",
    "org.junit.jupiter:junit-jupiter-params:${versions.junit}",
    "org.junit.platform:junit-platform-commons:${versions.junitPlatform}",
    "org.junit.platform:junit-platform-engine:${versions.junitPlatform}"
  ]
]

dependencies {
  constraints {
    implementation( 'org.bouncycastle:bcpkix-jdk18on:1.79' ) {
      because 'previous versions have a CVE impacting security'
    }
  }

  implementation group: 'org.bouncycastle', name: 'bcpkix-jdk18on', version: '1.79'

  implementation group: 'org.springframework.boot', name: 'spring-boot-starter'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-cache'

  implementation 'org.springframework:spring-expression:5.3.39'
  implementation 'org.springframework:spring-web:5.3.39'
  implementation 'org.springframework:spring-webmvc:6.1.14'

  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: '4.0.2'
  implementation group: 'org.springframework.cloud', name: 'spring-cloud-openfeign-core', version: '4.0.2'
  implementation group: 'io.github.openfeign', name: 'feign-httpclient', version: '12.3'

  implementation group: 'org.springframework.retry', name: 'spring-retry'
  implementation group: 'com.sendgrid', name: 'sendgrid-java', version: '4.9.3'
  implementation group: 'com.github.hmcts', name: 'core-case-data-store-client', version: '5.1.0'

  implementation group: 'commons-io', name: 'commons-io', version: '2.17.0'
  implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'

  implementation group: 'uk.gov.service.notify', name: 'notifications-java-client', version: '5.0.0-RELEASE'
  implementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: '5.3.2'
  implementation group: 'com.github.hmcts', name: 'idam-java-client', version: '2.1.2'
  implementation group: 'com.github.hmcts', name: 'send-letter-client', version: '3.0.4.1'

  implementation group: 'com.github.hmcts', name: 'document-management-client', version: '7.0.0'
  implementation group: 'com.github.hmcts', name: 'ccd-case-document-am-client', version: '1.7.1'

  implementation group: 'com.launchdarkly', name: 'launchdarkly-java-server-sdk', version: '6.2.1'
  implementation group: 'com.google.guava', name: 'guava', version: '32.0.1-jre'

  implementation group: 'ch.qos.logback', name: 'logback-core', version: '1.5.13'
  implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.5.13'
  implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.16'

  annotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
  compileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok

  testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
  testCompileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok

  testImplementation group: 'org.mockito', name: 'mockito-core', version: '4.2.0'
  testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '4.8.1'
  testImplementation group: 'org.mockito', name: 'mockito-inline', version: '4.8.1'

  testImplementation libraries.junit5
  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', {
    exclude group: 'junit', module: 'junit'
  }
}

 bootJar {
   enabled = false
 }

jar {
  manifest {
    attributes 'Implementation-Title': project.name, 'Implementation-Version': project.version
  }
  enabled = true
  archiveClassifier = ''
}

// Gradle 7.x issue, workaround from: https://github.com/gradle/gradle/issues/17236#issuecomment-894768083
//rootProject.tasks.named("processSmokeTestResources") {
//  duplicatesStrategy = 'include'
//}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

def pomConfig = {
  licenses {
    license {
      name "MIT License"
      url "https://www.opensource.org/licenses/MIT"
      distribution "repo"
    }
  }

  scm {
    url "https://github.com/hmcts/civil-commons"
  }
}


task sourcesJar(type: Jar, dependsOn: classes) {
  archiveClassifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  archiveClassifier = 'javadoc'
  from javadoc.destinationDir
}

//artifacts {
//  archives sourcesJar
//  archives javadocJar
//}

def gitRepo = "https://github.com/hmcts/civil-commons"

publishing {
  repositories {
    maven {
      name = "AzureArtifacts"
      url = uri("https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1")
      credentials {
        username = System.getenv("AZURE_DEVOPS_ARTIFACT_USERNAME")
        password = System.getenv("AZURE_DEVOPS_ARTIFACT_TOKEN")
      }
    }
  }
  publications {
    Main(MavenPublication) {
      artifact jar
      groupId project.group
      artifactId rootProject.name
      version project.version

      pom {
        name = 'Civil Commons Library'
        description = 'Civil Commons Library'
        url = gitRepo
        licenses {
          license {
            name = 'MIT License, Copyright (c) 2023 HM Courts & Tribunals Service'
            url = "https://www.opensource.org/licenses/MIT"
          }
        }
        scm {
          url = gitRepo
        }
      }
    }
  }
}


